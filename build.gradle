plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.14.0"
    id 'net.researchgate.release' version '2.8.1'
}

group 'io.github.vlsergey.springdatarestutils'

gradlePlugin {
    plugins {
	springdatarestutils {
	    id = 'io.github.vlsergey.spring-data-rest-utils'
	    implementationClass = 'io.github.vlsergey.springdatarestutils.PluginImpl'
	}
    }
}

ext {
    lombokVersion = '1.18.12'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.compilerArgs << '-parameters'
    options.debugOptions.debugLevel = "source,lines,vars"
    options.encoding = 'UTF-8'
    options.release = 11
}

compileTestJava {
    options.compilerArgs << '-parameters'
    options.debugOptions.debugLevel = "source,lines,vars"
    options.encoding = 'UTF-8'
    options.release = 11
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.12.3'
    implementation group: 'io.swagger.core.v3', name: 'swagger-models', version: '2.1.9'
    compileOnly group: 'org.reflections', name: 'reflections', version: '0.9.12'
    compileOnly group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.5.1'
    compileOnly group: 'org.springframework.data', name: 'spring-data-rest-core', version: '3.5.1'
    
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
    testImplementation(platform('org.junit:junit-bom:5.7.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation group: 'org.openapi4j', name: 'openapi-parser', version: '1.0.7'
    testImplementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    testImplementation group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.5.1'
    testImplementation group: 'org.springframework.data', name: 'spring-data-rest-core', version: '3.5.1'
}

test {
    useJUnitPlatform()
    testLogging {
	events "passed", "skipped", "failed"
    }
}

task regenerateReadme {
   doLast {
      def releaseVersion = project.property('release.releaseVersion');
      copy {
          from file( 'README.template.md')
          rename 'README\\.template\\.md', 'README.md'
          into project.projectDir
          expand([ "version": releaseVersion ])
      }

      exec {
          workingDir project.projectDir
          commandLine 'git'
          args 'add', 'README.md'
      }

      exec {
          workingDir project.projectDir
          commandLine 'git'
          args 'commit', '-m', 'Update version in README.md to ' + releaseVersion
      }
   }
}
beforeReleaseBuild.dependsOn regenerateReadme

release {
    failOnCommitNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    pushReleaseVersionBranch = false
}

afterReleaseBuild.dependsOn publishPlugins

pluginBundle {
    website = 'https://github.com/vlsergey/spring-data-rest-utils'
    vcsUrl = 'https://github.com/vlsergey/spring-data-rest-utils.git'
    description = 'Gradle plug-in to generate different specifications (such as OpenAPI) from Spring Data REST entities and repositories'
    tags = ['spring', 'hateoas', 'rest', 'typescript', 'codegeneration', 'OpenAPI']

    mavenCoordinates {
	groupId = project.group.toString()
	artifactId = project.name
	version = project.version.toString()
    }

    plugins {
	springdatarestutils {
	    displayName = 'Generate OpenAPI specification from Spring Data REST entities and repositories'
	}
    }
}
